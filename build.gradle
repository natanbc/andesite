plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
	id 'com.palantir.docker' version '0.22.1'
}

def versionObj = new Version(major: 0, minor: 18, revision: 2)

sourceCompatibility = 11
targetCompatibility = 11

allprojects {
    group 'com.github.natanbc'
    version versionObj.toString()
}

ext {
    //@formatter:off
    configVersion =     '1.3.4'
    vertxWebVersion =   '3.7.0'
    magmaVersion =      '0.9.2'
    lavaplayerVersion = '1.3.17'
    lavadspVersion =    '0.5.2'
    jdaNasVersion =     '1.0.6'
    singyeongVersion =  '7c8ce56'
    logbackVersion =    '1.2.3'
    sentryVersion =     '1.7.23'
    prometheusVersion = '0.6.0'
    jsr305Version =     '3.0.2'
    
    jattachVersion =    'v1.5'
    //@formatter:on
}

mainClassName = 'andesite.node.Andesite'

repositories {
    jcenter()
    maven { url 'https://dl.bintray.com/natanbc/maven' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile project(':api')

    //Config
    compile "com.typesafe:config:$configVersion"
    
    //REST, WebSocket, etc
    compile "io.vertx:vertx-web:$vertxWebVersion"

    //Raw audio -> udp packet
    compile "club.minnced:magma:$magmaVersion"

    //Audio player
    compile "com.sedmelluq:lavaplayer:$lavaplayerVersion"

    //Audio filters
    compile "com.github.natanbc:lavadsp:$lavadspVersion"

    //Native send system
    compile "com.sedmelluq:jda-nas:$jdaNasVersion"

    //Singyeong connection support
    compile("com.github.singyeong:java-client:$singyeongVersion") {
        exclude group: 'org.projectlombok', module: 'lombok'
    }

    //Logger implementation
    compile "ch.qos.logback:logback-classic:$logbackVersion"

    //Error tracking
    compile "io.sentry:sentry:$sentryVersion"
    compile "io.sentry:sentry-logback:$sentryVersion"

    //Metrics
    compile "io.prometheus:simpleclient:$prometheusVersion"
    compile "io.prometheus:simpleclient_hotspot:$prometheusVersion"
    compile "io.prometheus:simpleclient_logback:$prometheusVersion"

    //Code safety
    compile "com.google.code.findbugs:jsr305:$jsr305Version"
}

docker {
    name "natanbc/andesite:${getCommitHash()}"
    def branch = getBranchName()
    if(branch != null && !branch.trim().isEmpty()) {
        tag 'latest-current-branch', "natanbc/andesite:${branch}"
    }
    //only set latest tag for releases, so `docker pull natanbc/andesite`
    //pulls the latest stable version
    if(System.getenv("TAG") != null && !System.getenv("TAG").trim().isEmpty()) {
        tag 'versioned', "natanbc/andesite:${versionObj}"
        tag 'latest', 'natanbc/andesite:latest'
    }
    files "build/libs/andesite-node-${versionObj}-all.jar",
            project(':jattach-debug-plugin').getTasksByName('jar', false).outputs,
            project(':jfr-debug-plugin').getTasksByName('jar', false).outputs,
            "jlink.sh"
    buildArgs([
        version: versionObj.toString(),
        jattachVersion: jattachVersion
    ])
}

def natives = [
        'all',
        'darwin',
        'linux-arm',
        'linux-x86',
        'linux-x86-64',
        'win-x86',
        'win-x86-64'
]

task buildAll {}

tasks.docker.dependsOn buildAll
buildAll.dependsOn ':jattach-debug-plugin:jar'
buildAll.dependsOn ':jfr-debug-plugin:jar'
buildAll.dependsOn ':api:jar'
buildAll.dependsOn ':api:javadoc'

task ci {
    dependsOn buildAll
    dependsOn tasks.docker
    dependsOn dockerTag
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

jar {
    manifest {
        attributes(
                'Main-Class': mainClassName
        )
    }
}

natives.each { arch ->
    def shadowTask = task("shadow-${arch}", type: ShadowJar) {
        classifier = arch

        from project.sourceSets.main.output
        configurations = [ project.configurations.runtime ]
        manifest.inheritFrom project.tasks.jar.manifest

        exclude 'lombok/**'
        exclude 'vertx-web-js/**'
        exclude 'vertx-web/**'
        exclude 'vertx-auth-common-js/**'
        exclude 'vertx-auth-common/**'
        exclude 'vertx-bridge-common-js/**'
        exclude 'vertx-bridge-common/**'
        exclude 'vertx-core/**'
        exclude 'Class50/**'
        exclude 'com/zwitserloot/**'
        exclude 'secondaryLoading.SCL.lombok/**'
        if(arch != 'all') {
            natives.each {
                if(it != arch) {
                    exclude "natives/${it}/**"
                }
            }
        }
    }
    buildAll.dependsOn shadowTask
}

def lint = [
        "auxiliaryclass",
        "cast",
        "deprecation",
        "dep-ann",
        "divzero",
        "empty",
        "exports",
        "fallthrough",
        "finally",
        "module",
        "opens",
        "options",
        "overloads",
        "overrides",
        "path",
        "rawtypes",
        "removal",
        "static",
        "try",
        "unchecked",
        "varargs",
        "preview"
]

import org.apache.tools.ant.filters.ReplaceTokens

task sourcesForRelease(type: Copy) {
    from ('src/main/java') {
        include '**/Version.java'
        filter(ReplaceTokens, tokens: [
                VERSION_MAJOR:    versionObj.major,
                VERSION_MINOR:    versionObj.minor,
                VERSION_REVISION: versionObj.revision,
                COMMIT:           getCommitHash(),
                BUILD_NUMBER:     getBuildNumber()
        ])
    }
    into 'build/filteredSrc'

    includeEmptyDirs = false
}

task generateJavaSources(type: SourceTask) {
    def javaSources = sourceSets.main.allJava.filter {
        it.name != 'Version.java'
    }
    source = javaSources + sourcesForRelease.destinationDir

    dependsOn sourcesForRelease
}

compileJava {
    source = generateJavaSources.source

    dependsOn generateJavaSources
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.incremental = true
    options.compilerArgs += ["-Xlint:${lint.join(",")}", "-Werror"]
}

compileTestJava.enabled = false
processTestResources.enabled = false

class Version {
    String major, minor, revision

    String toString() {
        "$major.$minor.$revision"
    }
}

static def getCommitHash() {
    def p = Runtime.getRuntime().exec("git rev-parse HEAD")
    p.waitFor()
    p.getIn().text.trim()
}

static def getBranchName() {
    def b = System.getenv("REAL_BRANCH")
    if(b == null) {
        return null
    }
    b.split("\n")[0]
}

def getBuildNumber() {
    def properties = [
            "BUILD_NUMBER",
            "TRAVIS_BUILD_NUMBER"
    ]
    for(name in properties) {
        String property
        if(hasProperty(name)) {
            property = project.properties[name]
        } else {
            property = System.getenv(name) ?: ""
        }
        if(property != "") {
            return property
        }
    }
    return "unable to find build number"
}
