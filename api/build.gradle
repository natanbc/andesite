plugins {
    id 'java-library'
    id 'maven'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

archivesBaseName = 'andesite-api'

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
}

import org.gradle.external.javadoc.StandardJavadocDocletOptions

javadoc {
    failOnError = false
    options.encoding = "UTF-8"
    options.addBooleanOption('html5', true)
    options.addBooleanOption("-no-module-directories", true)
    if(options instanceof StandardJavadocDocletOptions) {
        options
                .linkSource()
                .noDeprecated()
                .links(
                    "https://vertx.io/docs/$vertxWebVersion/apidocs/",
                    "https://static.javadoc.io/com.google.code.findbugs/jsr305/$jsr305Version/",
                    "https://natanbc.github.io/lavadsp/"
                )
                .charSet("UTF-8")
                .docEncoding("UTF-8")
                .jFlags("-Dhttp.agent=javadoc")
    }
}

dependencies {
    //REST, WebSocket, etc
    api "io.vertx:vertx-web:$vertxWebVersion"

    //Audio player
    api "com.sedmelluq:lavaplayer:$lavaplayerVersion"

    //Audio filters
    api "com.github.natanbc:lavadsp:$lavadspVersion"
    
    //Singyeong connection support
    api("com.github.singyeong:java-client:$singyeongVersion") {
        exclude group: 'org.projectlombok', module: 'lombok'
    }

    //Code safety
    api "com.google.code.findbugs:jsr305:$jsr305Version"
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

publishing {
    publications {
        BintrayRelease(MavenPublication) {
            from components.java
            groupId 'com.github.natanbc'
            artifactId 'andesite-api'

            artifact sourceJar
            artifact javadocJar
        }
    }
}

bintray {
    version = project.version
    user = getProjectProperty("BINTRAY_USERNAME")
    key = getProjectProperty("BINTRAY_API_KEY")
    publications = ["BintrayRelease"]
    pkg {
        repo = 'maven'
        name = 'andesite-api'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/natanbc/andesite-node.git'
        publish = true
        version {
            name = project.version.toString()
            released = new Date()
        }
    }
}

String getProjectProperty(String propertyName) {
    String property = ""
    if(hasProperty(propertyName)) {
        property = project.properties[propertyName]
    } else {
        property = System.getenv(propertyName) ?: ""
    }
    return property
}

bintrayUpload {
    dependsOn build

    onlyIf { !getProjectProperty("bintrayUsername").empty }
    onlyIf { !getProjectProperty("bintrayApiKey").empty }
}